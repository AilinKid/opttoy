[ExpandSelectExists, Normalize]
(Select
    $input:*
    $filter:(FilterList
        $list:[ ... $exists:(Exists $subquery:*) ... ]
    )
)
=>
(SemiJoinApply
    $input
    $subquery
    (FilterList (RemoveListItem $list $exists))
)

[ExpandSelectNotExists, Normalize]
(Select
    $input:*
    $filter:(FilterList
        $list:[ ... $exists:(Not (Exists $subquery:*)) ... ]
    )
)
=>
(AntiJoinApply
    $input
    $subquery
    (FilterList (RemoveListItem $list $exists))
)

[HoistJoinProject, Normalize]
(JoinApply
    $left:*
    $right:(Project $input:* $projections:*) && (IsCorrelated $right $left)
    $filter:*
)
=>
(Select
    (Project
        (OpName
            $left
            $input
            (True)
        )
        (AppendColumnProjections $projections $left)
    )
    $filter
)

[HoistJoinGroupBy, Normalize]
(JoinApply
    $left:*
    $right: (GroupBy
        $input:*
        (Projections $items:* && (IsEmptyList $items))
        $aggregations:*
    ) && (IsCorrelated $right $left)
    $filter:*
)
=>
(Select
    (GroupBy
        (LeftJoinApply
            $left
            $input
            (True)
        )
        (ColumnProjections $left)
        $aggregations
    )
    $filter
)

[DecorrelateJoin, Normalize]
(JoinApply
    $left:*
    $right:* && ^(IsCorrelated $right $left)
    $filter:*
)
=>
(NonJoinApply $optype $left $right $filter)

[HoistJoinFilterSubquery, Normalize]
(Join
    $left:*
    $right:*
    (FilterList
        $list:[ ... $subquery:(Subquery $subqueryInput:* $projection:*) ... ]
    )
)
=>
(InnerJoinApply
    (OpName
        $left
        $right
        (True)
    )
    $subqueryInput
    (FilterList (ReplaceListItem $list $subquery $projection))
)

[HoistSelectFilterSubquery, Normalize]
(Select
    $input:*
    (FilterList
        $list:[... $subquery:(Subquery $subqueryInput:* $projection:*) ...]
    )
)
=>
(InnerJoinApply
    $input
    $subqueryInput
    (FilterList (ReplaceListItem $list $subquery $projection))
)
